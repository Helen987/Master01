USE [BI_DM]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO





CREATE VIEW [DCP].[VF_KPI_QVW]
AS

SELECT
	 WBS
	, DELIBERATION_CD
	, CALENDAR_YM
	, CALENDAR_YM AS RECORD_DATE
	, CAST('$QVW$' AS NVARCHAR(18)) AS ORDER_PART_NO_CD
	, CAST('$QVW$' AS NVARCHAR(10)) AS SCM_CUSTOMER_CD
	, REVISION_ID
	, HORIZON_ID

	--既存メジャー
	/* 未来分の数量･金額･GPはDCPデータを
	   過去分は実績値を集計するように0をセットする */
	, CASE 
		WHEN REVISION_ID=1 AND CALENDAR_YM >= FORMAT(GETDATE(), 'yyyy-MM-01') THEN VOLUME
		WHEN REVISION_ID=1 AND CALENDAR_YM < FORMAT(GETDATE(), 'yyyy-MM-01') THEN 0
		ELSE VOLUME
		END	AS VOLUME
	, CASE 
		WHEN REVISION_ID=1 AND CALENDAR_YM >= FORMAT(GETDATE(), 'yyyy-MM-01') THEN REVENUE
		WHEN REVISION_ID=1 AND CALENDAR_YM < FORMAT(GETDATE(), 'yyyy-MM-01') THEN 0
		ELSE REVENUE
		END	AS REVENUE
	, PRICE
	, DEV_EXPENSE
	, PROFIT
	, PTI
	, ROI
	, CASE 
		WHEN REVISION_ID=1 AND CALENDAR_YM >= FORMAT(GETDATE(), 'yyyy-MM-01') THEN GP_AMT
		WHEN REVISION_ID=1 AND CALENDAR_YM < FORMAT(GETDATE(), 'yyyy-MM-01') THEN 0
		ELSE GP_AMT
		END	AS GP_AMT
	, CASE 
		WHEN REVISION_ID=1 AND CALENDAR_YM >= FORMAT(GETDATE(), 'yyyy-MM-01') THEN SC_AMT
		WHEN REVISION_ID=1 AND CALENDAR_YM < FORMAT(GETDATE(), 'yyyy-MM-01') THEN 0
		ELSE SC_AMT
		END	AS SC_AMT
	, WAFER
	, TTP
	
	--レポート用メジャー 1
	/*従来、HORIZON_CDで分類していたが、HORIZON_CDをディメンションで
	　もつことにより廃止*/

	--レポート用メジャー 2-1
	, CASE WHEN REVISION_ID = '1' THEN REVENUE ELSE 0 END AS RANKING_QVW

	--レポート用メジャー 3-1(内容は2-1と同じ)
	, CASE WHEN REVISION_ID = '1' THEN REVENUE ELSE 0 END AS BUDvsACT_QVW
	, CASE WHEN REVISION_ID = '1' THEN VOLUME ELSE 0 END AS BUDvsACT_QVW_VOL

	-- 代替キー
	,CONVERT(varchar(30),
		HASHBYTES('SHA1',
		CONCAT	(
			[WBS]
			,FORMAT(CALENDAR_YM, 'yyyyMMdd')
			,[DELIBERATION_CD]
			,[REVISION_ID]
			,'$QVW$'
			,'$QVW$')) ,1 ) as ALTKEY
	-- ホライズンラベル用
	,CASE [HORIZON_ID]
		WHEN 0 THEN 'Under Planning'
		WHEN 1 THEN 'Under Development'
		WHEN 2 THEN 'Under Mass-production'
	END as HORIZON_TXT
	-- リビジョンラベル用
	,CASE [REVISION_ID]
		WHEN 4 THEN 'Current DCP'
		WHEN 5 THEN 'Sales Plan as of Design Completion DCP'
		WHEN 6 THEN 'Sales Plan as of Plan DCP'
		ELSE [PRELIMINARY1]
	END as REVISION_TXT


	FROM F_DCP_KPI_QVW
	/*
		過去分の絞込みをすると、予実比較ができなくなってしまうので
		全データを取得するように変更*/
	/*WHERE 
		(CALENDAR_YM >= FORMAT(GETDATE(), 'yyyy-MM-01') AND REVISION_ID=1)
		OR
		(REVISION_ID IN (2,3))
	*/
	UNION ALL

	SELECT
	 CAST(ISNULL(DEVELOP_NO, '$ACT$') AS NVARCHAR(30)) WBS
	, CAST('$ACT$' AS NVARCHAR(6)) AS DELIBERATION_CD
	, CAST(FORMAT(RECORD_DATE, 'yyyy-MM-01') AS DATE) AS CALENDAR_YM
	, RECORD_DATE
	, CAST(ISNULL(B.ORDER_PART_NO_CD, '$ACT$') AS NVARCHAR(18)) AS ORDER_PART_NO_CD
	, CAST(ISNULL(B.SCM_CUSTOMER_CD, '$ACT$') AS NVARCHAR(10)) AS SCM_CUSTOMER_CD
	, '1' AS REVISION_ID
	, '2' AS HORIZON_ID

	--計画値の既存メジャーと同レイアウトで取得
	, GO_QTY AS VOLUME
	, GO_AMT_JPY AS REVENUE
	, '0' AS PRICE
	, '0' AS DEV_EXPENSE
	, '0' AS PROFIT
	, '0' AS PTI
	, '0' AS ROI
	, GO_AMT_JPY - GO_AMT_STD AS GP_AMT
	, GO_AMT_STD AS SC_AMT
	, '0' AS WAFER
	, '0' AS TTP

	--計画値のレポート用メジャーと同レイアウトで取得
	--レポート用メジャー 1
	/*従来、HORIZON_CDで分類していたが、HORIZON_CDをディメンションで
	　もつことにより廃止*/

	--レポート用メジャー 2-1
	, '0' AS RANKING_QVW

	--レポート用メジャー 3-1(内容は2-1と同じ)
	, '0' AS BUDvsACT_QVW
	, '0' AS BUDvsACT_QVW_VOL

	-- 代替キー
	,CONVERT(varchar(30),
		HASHBYTES('SHA1',
		CONCAT	(
			CAST(ISNULL(DEVELOP_NO, '$ACT$') AS NVARCHAR(30))
			,FORMAT(RECORD_DATE, 'yyyyMMdd')
			,'$ACT$'
			,'1'
			,B.ORDER_PART_NO_CD
			,B.SCM_CUSTOMER_CD)),1 ) as ALTKEY
	-- ホライズンラベル用
	,'Under Mass-production' as HORIZON_TXT
	-- リビジョンラベル用
	,(SELECT TOP 1 [PRELIMINARY1] FROM F_DCP_KPI_QVW WHERE REVISION_ID =1) AS REVISION_TXT

	FROM dbo.F_SAL_BILL_GO B

	LEFT JOIN dbo.V_MST_BOOK_PN P
	ON B.ORDER_PART_NO_CD = P.BOOKING_PN_CD
	WHERE
		B.RECORD_DATE < FORMAT(GETDATE(), 'yyyy-MM-01')












GO
